buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

plugins {
    id 'net.researchgate.release' version '2.6.0'
    id 'com.github.hierynomus.license' version '0.14.0'
    id 'io.codearte.nexus-staging' version '0.9.0'
    id 'org.dm.bundle' version '0.10.0'
}

ext {
    name = 'Camel Kafka ActivityStream Dataset'
    description = 'A Camel LDP implementation'
    vendor = 'pandorasystems'
    homepage = 'http://www.trellisldp.org'
    url = 'http://www.trellisldp.org/apidocs'
    license = 'Apache 2'
    inceptionYear = '2017'
}

configure(allprojects) { project ->
    apply plugin: 'application'
    apply plugin: 'checkstyle'
    apply plugin: 'maven'
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'org.dm.bundle'
    apply plugin: 'com.github.hierynomus.license'

    group = 'org.trellisldp.camel'

    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
    }

    ext {
        camelVersion = '2.20.0'
        camelLdpVersion = '0.1.0'
        commonsRdfVersion = '0.5.0'
        jsonldVersion = "0.11.1"
        thriftVersion = "0.9.3"
        trellisApiVersion = "0.6.0"

        /* Testing */
        junitVersion = '5.0.1'
        junitPlatformVersion = '1.0.1'
        jaxbVersion = '2.3.0'
        jacocoVersion = '0.7.9'
        jenaVersion = '3.5.0'
        activationVersion = '1.1.1'
        logbackVersion = '1.2.3'
        apiguardianVersion = '1.0.0'
        trellisIoJenaVersion = '0.2.1'

        camelVersionRange = '[2.16,3)'
        projectOsgiVersion = project.version.replaceAll("-SNAPSHOT", ".SNAPSHOT")
        defaultOsgiImports = 'org.osgi.service.blueprint;version="[1,2)",*'

        jacocoVersion = '0.8.0'
        checkstyleVersion = '8.8'
    }

    processResources {
        filesNotMatching(["**/*.sh", "**/*.jar", "**/*.tar", "**/*.xml"]) {
            expand(project.properties)
        }
    }
}

configure(subprojects) { subproject ->
    apply plugin: 'com.bmuschko.nexus'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'checkstyle'
    apply plugin: 'jacoco'

    sourceCompatibility = 1.9
    targetCompatibility = 1.9

    checkstyle {
        configFile = rootProject.file('buildtools/src/main/resources/checkstyle/checkstyle.xml')
        configProperties.checkstyleConfigDir = rootProject.file('buildtools/src/main/resources/checkstyle/')
        toolVersion = checkstyleVersion
    }

    license {
        include "**/*.java"
        header rootProject.file('buildtools/src/main/resources/license/HEADER.txt')
        strictCheck true
        mapping {
            java = 'SLASHSTAR_STYLE'
        }
    }
}

project("serializer") {
    description = 'A Camel Kafka service'
    mainClassName = 'org.ubl.trellisldp.serialization.KafkaEventConsumer'

    dependencies {
        implementation group: 'org.apache.camel', name: 'camel-core', version: camelVersion
        implementation group: 'org.trellisldp', name: 'camel-ldp', version: camelLdpVersion
        implementation group: 'org.apache.camel', name: 'camel-kafka', version: camelVersion
        implementation group: 'org.apache.camel', name: 'camel-jackson', version: camelVersion
        implementation group: 'org.apache.jena', name: 'jena-osgi', version: jenaVersion
        implementation group: 'org.apache.commons', name: 'commons-rdf-api', version: commonsRdfVersion
        implementation group: 'org.trellisldp', name: 'trellis-api', version: trellisApiVersion
        implementation group: 'org.apache.commons', name: 'commons-rdf-jena', version: commonsRdfVersion
        implementation group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
        compile group: 'org.apache.jena', name: 'jena-fuseki-embedded', version: '3.5.0'
        testImplementation group: 'org.apache.jena', name: 'jena-osgi', version: jenaVersion
        testCompile group: 'org.apache.jena', name: 'jena-fuseki-embedded', version: '3.5.0'
        testImplementation group: 'org.trellisldp', name: 'trellis-io-jena', version: trellisIoJenaVersion
        testImplementation group: 'org.apache.camel', name: 'camel-kafka', version: camelVersion
        testImplementation group: 'org.apache.camel', name: 'camel-jackson', version: camelVersion
        testImplementation group: 'org.apache.camel', name: 'camel-http4', version: camelVersion
        testImplementation group: 'org.apache.camel', name: 'camel-exec', version: camelVersion
        testImplementation group: 'org.apiguardian', name: 'apiguardian-api', version: apiguardianVersion
        testImplementation group: 'org.junit.platform', name: 'junit-platform-runner', version: junitPlatformVersion
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion
        testImplementation group: 'org.apache.camel', name: 'camel-test', version: camelVersion
        testImplementation group: 'javax.xml.bind', name: 'jaxb-api', version: jaxbVersion
        testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
        testCompile group: 'org.apache.camel', name: 'camel-test-blueprint', version: camelVersion
        testImplementation group: 'javax.activation', name: 'activation', version: activationVersion
    }
}


